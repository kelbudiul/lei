program        → function*
function       → "fn" type IDENTIFIER "(" parameters? ")" block
parameters     → parameter ("," parameter)*
parameter      → IDENTIFIER ":" type

# Types
type           → basicType | arrayType
basicType      → "int" | "float" | "bool" | "str"
arrayType      → basicType "[" NUMBER? "]"

# Statements
block          → "{" statement* "}"
statement      → varDecl 
               | ifStmt 
               | whileStmt 
               | returnStmt 
               | exprStmt

varDecl        → "var" IDENTIFIER ":" type ("=" initializer)? ";" # Zero-initialized by default
initializer    → expression 
               | arrayLiteral

ifStmt         → "if" expression block ("else" block)?
whileStmt      → "while" expression block
returnStmt     → "return" expression? ";"
exprStmt       → expression ";"


# Array initialization
initializer    → expression              # This includes function calls like malloc()
               | arrayLiteral
arrayLiteral   → "{" (expression ("," expression)*)? "}"
# Array access
arrayAccess    → IDENTIFIER "[" expression "]"

# Expressions
expression     → assignment
assignment     → (IDENTIFIER | arrayAccess) assignOp expression
               | logicalOr
assignOp       → "=" | "+=" | "-=" | "*=" | "/="

logicalOr      → logicalAnd ("||" logicalAnd)*
logicalAnd     → equality ("&&" equality)*
equality       → comparison (("==" | "!=") comparison)*
comparison     → term (("<" | "<=" | ">" | ">=") term)*
term           → factor (("+" | "-") factor)*
factor         → unary (("*" | "/") unary)*
unary          → ("!" | "-") unary | call
call           → primary ("(" arguments? ")" | "[" expression "]")*
primary        → NUMBER | STRING | "true" | "false" | "(" expression ")"
               | IDENTIFIER | arrayInitializer
               | arrayAllocation

arguments      → expression ("," expression)*

# Lexical Rules
NUMBER         → DIGIT+ ("." DIGIT+)?
STRING         → "\"" <any char except "\"">* "\""
IDENTIFIER     → ALPHA (ALPHA | DIGIT)*
ALPHA          → "a"..."z" | "A"..."Z" | "_"
DIGIT          → "0"..."9"